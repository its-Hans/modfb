plugins {
    id 'org.jetbrains.kotlin.jvm' version "${kotlin_version}"
    id 'scala'

    id 'fabric-loom' version "${loom_version}"
}

java {
    sourceCompatibility = targetCompatibility = java_version
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = Integer.valueOf(java_version)
}

repositories {
    maven { url='https://maven.krysztal.dev/releases' }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings "net.fabricmc:yarn:${yarn_mappings}:v2"

    modImplementation "net.fabricmc:fabric-loader:${loader_version}"

    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

    modImplementation "net.fabricmc:fabric-language-kotlin:${fabric_kotlin_version}"
    modImplementation "dev.krysztal:krysztal-language-scala:${krysztal_scala_version}"
}

loom {
    splitEnvironmentSourceSets()

    mixin {
        useLegacyMixinAp = false
    }
}

def declInputs(TaskInputs inputs, Iterable<String> properties) {
    inputs.properties(
            properties.collectEntries {
                [it, project[it]]
            }
    )
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.FAIL

    declInputs(inputs, [
            "name",
            "modid",
            "version",
            "minecraft_version",
            "loader_version",
            "fabric_version",
            "fabric_kotlin_version",
            "krysztal_scala_version"
    ])

    filesMatching('fabric.mod.json') {
        expand(inputs.properties) {
            escapeBackslash = true
        }
    }
}